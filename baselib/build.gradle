apply plugin: 'com.android.library'
/**引入代码检查*/
apply from: '../config/quality.gradle'
/**引用发布脚本*/
apply from: 'deploy.gradle'

/**指定gradle以utf8编码进行java编译*/
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode Integer.parseInt(VERSION_CODE)
        versionName new StringBuffer().append(VERSION_NAME).append("-").append(COMPILE_TYPE).toString()
    }

    /**构建打包配置*/
    buildTypes {
        debug {
            //是否允许调试
            debuggable true
        }

        release {
            //是否允许调试
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7 //由于Android Studio1.4目前编译的源码最高只支持1.7，1.8还没有完全支持
        targetCompatibility JavaVersion.VERSION_1_7
    }
    /*************************编译libs目录下的so文件，如百度定位***************************/
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

/**
 * 项目依赖全局配置
 * Force Android Studio to Update Maven Snapshot Dependencies
 * */
configurations.all {
    // check for updates every build（实时更新snapshot版本）
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    //强制引用23.0.0版本，避免androidTestCompile 'com.android.support.test:runner:0.4' 自动引用23.0.1
    resolutionStrategy.force 'com.android.support:support-annotations:23.0.0'
}

/**项目依赖配置*/
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:support-v4:22.2.0'
    //瀑布流
    compile 'com.android.support:recyclerview-v7:22.2.0'
    //依赖注入
    compile 'com.jakewharton:butterknife:7.0.1'
    //数据库
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.j256.ormlite:ormlite-core:4.48'
    //动画
    compile 'com.nineoldandroids:library:2.4.0'
    //图片加载工具-远程/本地
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'

    //圆形头像  源码地址-----> https://github.com/hdodenhof/CircleImageView
    compile 'de.hdodenhof:circleimageview:1.3.0'

    //异步响应式编程库  源码地址-----> https://github.com/ReactiveX/RxAndroid
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.4'

    //加密键值存储，替代SharedPreferences来存储关键数据
    //源码地址-----> https://github.com/orhanobut/hawk
    compile 'com.orhanobut:hawk:1.18'

    //EventBus是一款针对Android 优化的发布/订阅事件总线。
    //源码地址-----> https://github.com/greenrobot/EventBus
    compile 'de.greenrobot:eventbus:2.4.0'

    //磁盘缓存库
    //源码地址-----> https://github.com/JakeWharton/DiskLruCache
    compile 'com.jakewharton:disklrucache:2.0.2'
}
